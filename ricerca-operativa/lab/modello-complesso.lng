! modello comlesso: sommatorie, gruppi di vincoli, ecc...;

TITLE ESERCIZIO 1.39;

SETS:

!INSIEMI PRIMITIVI;

MAGAZZINI / M1..M3 /: D;
PUNTI_VEND / P1..P4 /: F, R, Y; !;

!INSIEMI DERIVATI;

MP(MAGAZZINI, PUNTI_VEND): C, X;

ENDSETS

! DATI;

DATA:

D = 100 200 150; ! Disponibilita';

F = 500 300 200 400; ! Costi fissi;

R = 100 150 200 220; ! Richieste;

C = 23 10 12 20
    13 15 25 15
    30 21 10 16;

ENDDATA

! FUNZIONE OBIETTIVO;

MIN = @SUM(PUNTI_VEND(j): F(j) * Y(j)) +
      @SUM(MAGAZZINI(i): @SUM(PUNTI_VEND(j): C(i, j) * X(i, j)));

! VINCOLI;

@SUM(PUNTI_VEND(j): Y(j)) = 2;

@FOR (MAGAZZINI(i): @SUM(PUNTI_VEND(j): X(i, j)) <= D(i));
@FOR (PUNTI_VEND(j): @SUM(MAGAZZINI(i): X(i, j)) = R(j) * Y(j));

@FOR (MAGAZZINI(i): @FOR(PUNTI_VEND(j): @GIN(X(i, j))));
@FOR (PUNTI_VEND(j): @BIN(Y(j)));
